#!/usr/bin/env yarn repl -s

-- This script tests a hypothetical upgrade with manual ARS claims and manual ARS speed setting

PrintTransactionLogs
Alias ArsHolder "0xC89b6f0146642688bb254bF93C28fcCF1E182C81"
Alias USDCWhale "0x3dfd23a6c5e8bbcfc9581d2e864a68feb6a076d3"
Alias DAIWhale "0x47ac0Fb4F2D84898e4D9E7b4DaB3C24507a6D503"
Alias BigBorrower "0x847956f7f7ff49714fb2d70a0d0cd44a6376990f"
Web3Fork "https://mainnet-eth.aquarius.loan/@11458477" (ArsHolder USDCWhale DAIWhale)
UseConfigs mainnet

-- Deploy the flywheel impl
ComptrollerImpl Deploy Standard ComptrollerG7

-- Baseline USDC ars speed before the patch
Assert Equal (Comptroller ArsSpeed aUSDC) 5.3786477206671197e16

-- Mint tokens
From DAIWhale (Trx GasPrice 0 (Erc20 DAI Approve aDAI UInt256Max))
From DAIWhale (Trx GasPrice 0 (AToken aDAI Mint 10000e6))
From USDCWhale (Trx GasPrice 0 (Erc20 USDC Approve aUSDC UInt256Max))
From USDCWhale (Trx GasPrice 0 (AToken aUSDC Mint 10000e6))

-- Baseline ARS claim speed
Assert Equal (Comptroller CheckIsComped aDAI) True

Comptroller ClaimArs BigBorrower
AdvanceBlocks 1000
Expect Changes (Erc20 Ars TokenBalance BigBorrower) 2.893496802261224189e18
Comptroller ClaimArs BigBorrower

-- Propose to apply the patch

From ArsHolder (Ars Delegate ArsHolder)
From ArsHolder (Governor GovernorAlpha Propose "Disable automatic ars speed refresh and automatic claims and change ars speed" [(Address Unitroller) (Address ComptrollerG7) (Address Unitroller)] [0 0 0] ["_setPendingImplementation(address)" "_become(address)" "_setArsSpeed(address,uint256)"] [[(Address ComptrollerG7)] [(Address Unitroller)] [(Address aUSDC) 30000000000000000]])

-- Vote for, queue, and execute the proposal

MineBlock
From ArsHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 150000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute
ComptrollerImpl ComptrollerG7 MergeABI

Comptroller ClaimArs BigBorrower
AdvanceBlocks 1000
Expect Changes (Erc20 Ars TokenBalance BigBorrower) 2.893496802261224189e18
Comptroller ClaimArs BigBorrower

Print "Upgrade OK!"
